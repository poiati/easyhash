/*
Copyright 2011 Paulo Poiati

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.easyhash'
version = '1.0'
artifact = 'easyhash'

bundleDir = "$buildDir/bundle"

repositories {
    mavenCentral()
}

dependencies {
    groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.8.5'
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

task bundle(dependsOn: [ 'build', 'source', 'groovydoc', 'pom' ]) << {
  ant.copy(file: "$buildDir/libs/$artifact-${version}.jar", todir: bundleDir)
  ant.copy(file: "$buildDir/$artifact-$version-sources.jar", todir: bundleDir)
  ant.copy(file: "$buildDir/pom.xml", todir: bundleDir)
  ant.jar(destfile: "$bundleDir/$artifact-$version-javadoc.jar") {
    fileset (dir: "$buildDir/docs/groovydoc")
  }
  sign(file(bundleDir).listFiles());
  ant.jar(destfile: "$bundleDir/$artifact-$version-bundle.jar", basedir: file(bundleDir))
}

task source << {
    ant.jar(destfile: "$buildDir/$artifact-${version}-sources.jar") {
        fileset (dir: 'src/main/groovy', includes: '**/*.groovy')
    }
}

task pom << {
    pom {
        project {
            name artifact
            description 'The easiest way to generate HEX hashs in Groovy and Java'
            url 'http://blog.paulopoiati.com'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url 'http://github.com/poiati/easyhash'
            }
            developers {
                developer {
                    name 'Paulo Poiati'
                    email 'paulogpoiati@gmail.com'
                    url 'http://blog.paulopoiati.com'
                }
            }
        }
    }.writeTo("$buildDir/pom.xml")
}